apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.0'
    defaultConfig {
        applicationId "dev.dworks.apps.acrypto"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 11
        versionName "0.7"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    useLibrary 'org.apache.http.legacy'
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard/proguard-project.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard/proguard-project.pro'
        }
    }

    flavorDimensions "release", "default"
    productFlavors {
        main {
            applicationId "dev.dworks.apps.acrypto"
            dimension "default"
        }
        underground {
            applicationId "dev.dworks.apps.acrypto.underground"
            dimension "default"
        }
        google {
            dimension "release"
        }
        amazon {
            dimension "release"
        }
        other {
            dimension "release"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("underground") && (names.contains("google") || names.contains("other"))) {
            variant.ignore = true
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode true
    }
}

ext {
    supportLibVersion = '25.4.0'
    gmsVersion = '11.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.google.firebase:firebase-messaging:${gmsVersion}"
    compile "com.google.firebase:firebase-auth:${gmsVersion}"
    compile "com.google.firebase:firebase-crash:${gmsVersion}"
    compile "com.google.firebase:firebase-perf:${gmsVersion}"
    compile "com.google.firebase:firebase-database:${gmsVersion}"
    compile "com.google.firebase:firebase-ads:${gmsVersion}"
    compile "com.google.firebase:firebase-config:${gmsVersion}"
    compile "com.google.firebase:firebase-invites:${gmsVersion}"
    compile 'com.firebaseui:firebase-ui-auth:2.0.1'
    compile 'com.firebaseui:firebase-ui-database:2.0.1'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'dev.dworks.libs:volleyplus:0.1.4'
    compile 'cat.ereza:customactivityoncrash:2.1.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    compile 'org.fabiomsr:moneytextview:1.1.0'
    compile 'com.jaredrummler:material-spinner:1.1.0'
    compile 'com.zsoltsafrany:needle:1.0.0'
    compile 'com.anjlab.android.iab.v3:library:1.0.41'
    compile 'com.github.lykmapipo:local-burst:v0.2.0'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("keystore.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'
