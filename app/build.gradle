apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.0'
    defaultConfig {
        applicationId "dev.dworks.apps.acrypto"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 13
        versionName "0.9"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    useLibrary 'org.apache.http.legacy'
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard/proguard-project.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard/proguard-project.pro'
        }
    }

    flavorDimensions "release"
    productFlavors {
        google {
            dimension "release"
        }
        amazon {
            dimension "release"
        }
        other {
            dimension "release"
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode true
    }
}

ext {
    supportLibVersion = '25.4.0'
    gmsVersion = '11.0.2'
}

dependencies {
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.google.firebase:firebase-perf:${gmsVersion}"
    compile "com.google.firebase:firebase-database:${gmsVersion}"
    compile "com.google.firebase:firebase-ads:${gmsVersion}"
    compile 'com.firebaseui:firebase-ui-auth:2.1.0'
    compile 'com.firebaseui:firebase-ui-database:2.1.0'
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'dev.dworks.libs:volleyplus:0.1.4'
    compile 'cat.ereza:customactivityoncrash:2.1.0'
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    compile 'org.fabiomsr:moneytextview:1.1.0'
    compile 'com.jaredrummler:material-spinner:1.1.0'
    compile 'com.zsoltsafrany:needle:1.0.0'
    compile 'com.github.lykmapipo:local-burst:v0.2.0'
    compile 'com.github.javiersantos:AppUpdater:2.6.1'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.2'
    compile 'org.jsoup:jsoup:1.10.3'
    googleCompile "com.google.firebase:firebase-crash:${gmsVersion}"
    googleCompile "com.google.firebase:firebase-invites:${gmsVersion}"
    googleCompile "com.google.firebase:firebase-config:${gmsVersion}"
    googleCompile "com.google.firebase:firebase-messaging:${gmsVersion}"
    googleCompile "com.google.firebase:firebase-auth:${gmsVersion}"
    googleCompile 'com.anjlab.android.iab.v3:library:1.0.42'
    amazonCompile fileTree(include: ['*.jar'], dir: 'libs')
    amazonCompile 'com.github.eggheadgames:android-in-app-payments:+'
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("keystore.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'
