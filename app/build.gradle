apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0 rc2'
    defaultConfig {
        applicationId "dev.dworks.apps.acrypto"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 10
        versionName "0.6"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    useLibrary 'org.apache.http.legacy'
    signingConfigs {
        release
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard/proguard-project.pro',
                    'proguard/proguard-google-play-services.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard/proguard-project.pro',
                    'proguard/proguard-google-play-services.pro'
        }
    }

    flavorDimensions "release", "default"
    productFlavors {
        main {
            applicationId "dev.dworks.apps.acrypto"
            dimension "default"
        }
        underground {
            applicationId "dev.dworks.apps.acrypto.underground"
            dimension "default"
        }
        google {
            dimension "release"
        }
        amazon {
            dimension "release"
        }
        other {
            dimension "release"
        }
    }
    variantFilter { variant ->
        def names = variant.flavors*.name

        if (names.contains("underground") && (names.contains("google") || names.contains("other"))) {
            variant.ignore = true
        }
    }
}

ext {
    supportLibVersion = '25.4.0'
    gmsVersion = '11.0.1'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"
    compile "com.google.firebase:firebase-messaging:${gmsVersion}"
    compile "com.google.firebase:firebase-ads:${gmsVersion}"
    compile "com.google.firebase:firebase-invites:${gmsVersion}"
    compile "com.google.firebase:firebase-auth:${gmsVersion}"
    compile "com.google.firebase:firebase-crash:${gmsVersion}"
    compile "com.google.firebase:firebase-perf:${gmsVersion}"
    compile "com.google.firebase:firebase-database:${gmsVersion}"
    compile 'com.firebaseui:firebase-ui-database:+'
    compile 'com.firebaseui:firebase-ui-auth:+'
    compile 'com.google.code.gson:gson:+'
    compile 'dev.dworks.libs:volleyplus:+'
    compile 'cat.ereza:customactivityoncrash:+'
    compile 'com.github.PhilJay:MPAndroidChart:+'
    compile 'org.fabiomsr:moneytextview:+'
    compile 'com.jaredrummler:material-spinner:+'
    compile 'com.zsoltsafrany:needle:+'
    compile 'com.anjlab.android.iab.v3:library:+'
    testCompile 'junit:junit:4.12'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def props = new Properties()
props.load(new FileInputStream(rootProject.file("keystore.properties")))
android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
android.signingConfigs.release.storePassword props.keyStorePassword
android.signingConfigs.release.keyAlias props.keyAlias
android.signingConfigs.release.keyPassword props.keyAliasPassword

apply plugin: 'com.google.gms.google-services'
